buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'
    }
}

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'de.mannodermaus.android-junit5'
}

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', 'HEAD', '--count'
            standardOutput = stdout
        }
        return Integer.valueOf(stdout.toString().trim())
    } catch (ignored) {
        return null
    }
}

android {
    defaultConfig {
        compileSdk 34
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 41
        versionName "4.0.0"
        applicationId "de.storchp.opentracks.osmplugin"

        testInstrumentationRunnerArguments runnerBuilder: 'de.mannodermaus.junit5.AndroidJUnit5Builder'
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        nightly {
            if (System.getProperty("nightly_store_file") != null) {
                storeFile file(System.getProperty("nightly_store_file"))
                storePassword System.getProperty("nightly_store_password")
                keyAlias System.getProperty("nightly_key_alias")
                keyPassword System.getProperty("nightly_key_password")
            }
        }
    }
    compileOptions {
        // Sets Java compatibility to Java 17
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    productFlavors {
        full {
            applicationId "de.storchp.opentracks.osmplugin"
            buildConfigField "boolean", "offline", "false"
            flavorDimensions = ["default"]
        }
        offline {
            applicationId "de.storchp.opentracks.osmplugin.offline"
            buildConfigField "boolean", "offline", "true"
            flavorDimensions = ["default"]
        }
    }
    flavorDimensions = ["default"]
    sourceSets {
        full {
            manifest.srcFile "src/full/AndroidManifest.xml"
        }
        offline {
            manifest.srcFile "src/offline/AndroidManifest.xml"
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        nightly {
            signingConfig signingConfigs.nightly
            applicationIdSuffix ".nightly"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
    }
    lint {
        disable 'MissingTranslation'
    }
    namespace 'de.storchp.opentracks.osmplugin'
    applicationVariants.configureEach { variant ->
        variant.resValue "string", "applicationId", variant.applicationId

        if (variant.buildType.name == 'nightly') {
            variant.outputs.configureEach {
                setVersionCodeOverride(getVersionCode())
                setVersionNameOverride(versionName + "-" + versionCode)
            }
        }
        variant.outputs.configureEach {
            outputFileName = "${applicationId}_${variant.versionCode}.apk"
        }
    }
}

ext {
    mapsforgeVersion = '0.20.0'
    lifecycle_version = '2.7.0'
    junitVersion = '5.10.2'
    assertJVersion = '3.25.3'
}

dependencies {

    // Desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'

    // AndroidX
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.11.0'

    // VTM
    implementation "org.mapsforge:vtm:$mapsforgeVersion"
    implementation "org.mapsforge:vtm-themes:$mapsforgeVersion"
    implementation "org.mapsforge:vtm-http:$mapsforgeVersion"
    implementation "org.slf4j:slf4j-api:2.0.12"
    runtimeOnly "org.mapsforge:vtm-android:$mapsforgeVersion:natives-armeabi-v7a"
    runtimeOnly "org.mapsforge:vtm-android:$mapsforgeVersion:natives-arm64-v8a"
    runtimeOnly "org.mapsforge:vtm-android:$mapsforgeVersion:natives-x86"
    runtimeOnly "org.mapsforge:vtm-android:$mapsforgeVersion:natives-x86_64"
    implementation "org.mapsforge:vtm-android:$mapsforgeVersion"
    implementation "com.caverock:androidsvg:1.4"

    testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
    testImplementation "org.assertj:assertj-core:$assertJVersion"

    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"

    androidTestImplementation("androidx.test:runner:1.5.2")
    androidTestImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    androidTestImplementation("de.mannodermaus.junit5:android-test-core:1.4.0")

    androidTestRuntimeOnly("de.mannodermaus.junit5:android-test-runner:1.4.0")
}
