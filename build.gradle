apply plugin: 'com.android.application'

buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.2'
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven { url 'https://jitpack.io' }
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return null
    }
}

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', 'HEAD', '--count'
            standardOutput = stdout
        }
        return Integer.valueOf(stdout.toString().trim())
    } catch (ignored) {
        return null
    }
}

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 33
        versionCode 30
        versionName "2.16.0"
    }
    signingConfigs {
        nightly {
            if (System.getProperty("nightly_store_file") != null) {
                storeFile file(System.getProperty("nightly_store_file"))
                storePassword System.getProperty("nightly_store_password")
                keyAlias System.getProperty("nightly_key_alias")
                keyPassword System.getProperty("nightly_key_password")
            }
        }
    }
    compileOptions {
        // Sets Java compatibility to Java 11
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    productFlavors {
        full {
            applicationId "de.storchp.opentracks.osmplugin"
            buildConfigField "boolean", "offline", "false"
            flavorDimensions "default"
        }
        offline {
            applicationId "de.storchp.opentracks.osmplugin.offline"
            buildConfigField "boolean", "offline", "true"
            flavorDimensions "default"
        }
    }
    flavorDimensions "default"
    sourceSets {
        full {
            manifest.srcFile "src/full/AndroidManifest.xml"
        }
        offline {
            manifest.srcFile "src/offline/AndroidManifest.xml"
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        nightly {
            signingConfig signingConfigs.nightly
            applicationIdSuffix ".nightly"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'MissingTranslation'
    }
    buildFeatures {
        viewBinding true
    }
    applicationVariants.all { variant ->
        variant.resValue "string", "applicationId", variant.applicationId

        if (variant.buildType.name == 'nightly') {
            variant.outputs.all {
                setVersionCodeOverride(getVersionCode())
                setVersionNameOverride(getVersionName())
            }
        }
        variant.outputs.all {
            outputFileName = "${applicationId}_${variant.versionCode}.apk"
        }
    }
}

ext {
    mapsforgeVersion = '0.18.0'
    lifecycle_version = '2.5.1'
}

dependencies {
    // Desugaring
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.0'

    // AndroidX
    implementation "androidx.lifecycle:lifecycle-viewmodel:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation 'androidx.appcompat:appcompat:1.5.0'
    implementation 'androidx.documentfile:documentfile:1.0.1'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'com.google.android.material:material:1.6.1'

    // Other
    implementation "org.mapsforge:mapsforge-core:$mapsforgeVersion"
    implementation "org.mapsforge:mapsforge-map:$mapsforgeVersion"
    implementation "org.mapsforge:mapsforge-map-android:$mapsforgeVersion"
    implementation "org.mapsforge:mapsforge-map-reader:$mapsforgeVersion"
    implementation "org.mapsforge:mapsforge-themes:$mapsforgeVersion"
    implementation 'net.sf.kxml:kxml2:2.3.0'
    implementation 'com.caverock:androidsvg:1.4'
}
